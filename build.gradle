import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version('7.1.0')
}

group = project.property("group")
version = project.property("version")
sourceCompatibility = 16
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://maven.playpro.com' }
    maven { url = 'https://repo.bg-software.com/repository/api/' }
    maven { url = 'https://repo.bg-software.com/repository/public-libs/' }
    maven {
        name = "IntellectualSites Releases"
        url = uri("https://mvn.intellectualsites.com/content/repositories/releases/")
    }
    maven {
        name = "IntellectualSites Snapshots"
        url = uri("https://mvn.intellectualsites.com/content/repositories/snapshots/")
    }
    maven {
        name = "EngineHub"
        url = uri("https://maven.enginehub.org/repo/")
    }
    maven {
        name = "OSS Sonatype"
        url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
    }
}

dependencies {
    // Spigot jar
    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'
    // CoreProtect
    compileOnly 'net.coreprotect:coreprotect:20.0'
    // Plugin Hooks
    compileOnly 'com.wasteofplastic:AcidIsland:latest'
    compileOnly 'com.wasteofplastic:ASkyblock:latest'
    compileOnly 'world.bentobox:BentoBox:latest'
    compileOnly 'com.drtshock:FactionsUUID:latest'
    compileOnly 'com.drtshock:FactionsUUID-New:latest'
    compileOnly 'net.prosavage:FactionsX:latest'
    compileOnly 'me.ryanhamshire:GriefPrevention:latest'
    compileOnly 'com.massivecraft:Factions:latest'
    compileOnly 'com.massivecraft:MassiveCore:latest'
    compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:latest'
    compileOnly 'com.palmergames:Towny:latest'
    compileOnly 'com.stefthedev:Villages:latest'
    compileOnly 'com.plotsquared:PlotSquared-Core:6.1.2'
}

processResources {
    filter ReplaceTokens, tokens: [
            "project.name"       : project.property("name"),
            "project.groupId"    : project.property("groupid"),
            "project.artifactId" : project.property("artifactid"),
            "project.version"    : project.property("version"),
            "project.description": project.property("description")
    ]
}

jar {
    archiveFileName.set(project.property("name").toString() + "-lib.jar")
}

shadowJar {
    archiveFileName.set(project.property("name").toString() + "-${project.version}.jar")
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

build {
    dependsOn shadowJar
    dependsOn publish
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build

publishing {
    repositories {
        mavenLocal()
    }
}
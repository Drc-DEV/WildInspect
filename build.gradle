import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version('6.1.0')
}

group = project.property("group")
version = project.property("version")
sourceCompatibility = project.property("sourceversion")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://repo.bg-software.com/repository/api/' }
    maven { url 'https://repo.bg-software.com/repository/public-libs/' }
}

dependencies {
    // Spigot jar
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    // CoreProtect
    compileOnly 'net.coreprotect:CoreProtect-5:latest'
    compileOnly 'net.coreprotect:CoreProtect-6:latest'
    // Plugin Hooks
    compileOnly 'com.wasteofplastic:AcidIsland:latest'
    compileOnly 'com.wasteofplastic:ASkyblock:latest'
    compileOnly 'world.bentobox:BentoBox:latest'
    compileOnly 'com.drtshock:FactionsUUID:latest'
    compileOnly 'com.drtshock:FactionsUUID-New:latest'
    compileOnly 'net.prosavage:FactionsX:latest'
    compileOnly 'me.ryanhamshire:GriefPrevention:latest'
    compileOnly 'com.massivecraft:Factions:latest'
    compileOnly 'com.massivecraft:MassiveCore:latest'
    compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:latest'
    compileOnly 'com.palmergames:Towny:latest'
    compileOnly 'com.stefthedev:Villages:latest'
}

processResources {
    filter ReplaceTokens, tokens: [
            "project.name"       : project.property("name"),
            "project.groupId"    : project.property("groupid"),
            "project.artifactId" : project.property("artifactid"),
            "project.version"    : project.property("version"),
            "project.description": project.property("description")
    ]
}

jar {
    archiveFileName.set(project.property("name").toString() + "-lib.jar")

}

shadowJar {
    archiveFileName.set(project.property("name").toString() + "-${project.version}.jar")
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

build {
    dependsOn shadowJar
    dependsOn publish
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build

publishing {
    repositories {
        mavenLocal()
    }
}